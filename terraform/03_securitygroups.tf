# Traffic Internet -> ALB
resource "aws_security_group" "load-balancer" {
  name        = "load_balancer_security_group"
  description = "Access to the ALB"
  vpc_id      = aws_vpc.saleor-vpc.id

  ingress {
    from_port   = 8000
    to_port     = 8000
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# Traffic ALB -> ECS
resource "aws_security_group" "ecs" {
  name        = "ecs_security_group"
  description = "Inbound access from the ALB"
  vpc_id      = aws_vpc.saleor-vpc.id

  ingress {
    from_port       = 0
    to_port         = 0
    protocol        = "-1"
    security_groups = [aws_security_group.load-balancer.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# Traffic ECS -> RDS
resource "aws_security_group" "rds" {
  name        = "rds-security-group"
  description = "Inbound access from ECS to RDS PostgreSQL"
  vpc_id      = aws_vpc.saleor-vpc.id

  ingress {
    protocol        = "tcp"
    from_port       = "5432"
    to_port         = "5432"
    security_groups = [aws_security_group.ecs.id]
  }

  egress {
    protocol    = "-1"
    from_port   = 0
    to_port     = 0
    cidr_blocks = ["0.0.0.0/0"]
  }
}
